buildscript {
    ext {
        kotlinVersion = "1.2.21"
        junitPlatformVersion = "1.0.3"
        dokkaVersion = "0.9.16-eap-3"
        bintrayVersion = "1.7.3"
    }
    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayVersion"
    }
}

allprojects {
    ext {
        junitJupiterVersion = "5.0.1"
        mockitoVersion = "2.12.0"
        kotlinMockitoVersion = "1.5.0"
    }
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "org.junit.platform.gradle.plugin"
    apply plugin: "org.jetbrains.dokka"

    group "fr.cvlaminck.gradle"

    ext {
        bintrayUser = project.property('bintrayUser')
        bintrayKey = project.property('bintrayKey')
    }

    repositories {
        jcenter()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "com.nhaarman:mockito-kotlin-kt1.1:$kotlinMockitoVersion"

        testCompile "org.junit.platform:junit-platform-engine:$junitPlatformVersion"
        testCompile "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
        testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    }

    junitPlatform {
        filters {
            includeClassNamePattern '.*Test'
        }
        enableStandardTestTask true
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}
